subprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'
    group = 'com.github.epierce'
    version = '1.0.0'
    repositories {
        mavenCentral()
        mavenLocal()
    }

    project.ext {
        groovyVersion = '2.3.7'
        slf4jVersion = '1.7.2'
        gAdminDirVersion = 'directory_v1-rev47-1.19.0'
        gOAUTHVersion = 'v2-rev80-1.19.0'
        commonsCliVersion = '1.2'
        googleHTTPclientJackson = '1.19.0'
        googleOAUTHclient = '1.19.0'
    }

    dependencies {
        compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    }
}

project(':GoogleAppsClient') {
    apply plugin: 'maven'

    dependencies {
        compile "com.google.apis:google-api-services-admin-directory:${gAdminDirVersion}"
        compile "org.slf4j:slf4j-api:${project.slf4jVersion}"
        compile "org.slf4j:slf4j-log4j12:${project.slf4jVersion}"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "file://localhost/tmp/myRepo/")
                pom.project {
                    name 'GoogleAppsClient'
                    packaging 'jar'
                    description 'Groovy Client Library for GoogleApps Admin Directory API'
                    inceptionYear '2014'

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'epierce'
                            name 'Eric Pierce'
                            email 'epierce@usf.edu'
                            organization 'University of South Florida'
                            organizationUrl 'http://www.usf.edu'
                            roles {
                                role 'architect'
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}

project(':GoogleAppsTool') {
    apply plugin: 'application'

    mainClassName = 'com.github.epierce.GoogleAppsTool'

    dependencies {
        compile "com.google.apis:google-api-services-oauth2:${gOAUTHVersion}"
        compile "com.google.http-client:google-http-client-jackson2:${googleHTTPclientJackson}"
        compile "com.google.oauth-client:google-oauth-client-jetty:${googleOAUTHclient}"
        compile "commons-cli:commons-cli:${project.commonsCliVersion}"
        compile project(':GoogleAppsClient')
    }

    //Set the classpath to be '$APP_HOME:$APP_HOME/lib/*'
    startScripts {
        classpath = files('$APP_HOME/lib/*')

        doLast {
            def scriptFile = file "${outputDir}/${applicationName}"
            scriptFile.text = scriptFile.text.replace('CLASSPATH=$APP_HOME/lib/*', 'CLASSPATH=$APP_HOME:$APP_HOME/lib/*')
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}